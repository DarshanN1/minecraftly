buildscript {
    repositories {
        jcenter()

        maven {
            name = 'zt-public-snapshots'
            url = 'http://repos.zeroturnaround.com/nexus/content/groups/zt-public'
        }
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.1'
        classpath group: 'org.zeroturnaround', name: 'gradle-jrebel-plugin', version: '1.1.2'
    }
}


import org.apache.tools.ant.filters.ReplaceTokens

import java.nio.file.Files
import java.nio.file.StandardCopyOption

apply plugin: 'idea'

ext.bukkitVersion = '1.8.3-R0.1-SNAPSHOT'
ext.bungeeVersion = '1.8-SNAPSHOT'
ext.exhaustVersion = '1.0.0-SNAPSHOT'
ext.pmfVersion = '1.0.0-SNAPSHOT'

defaultTasks 'clean', 'build', 'test'

subprojects {
    apply plugin: 'java'
    apply plugin: 'rebel'
    apply plugin: 'com.github.johnrengelman.shadow'

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    ext.cleanName = getName()

    jar { // rename un-shadowed jar, adding the suffix 'original', much like how Maven handles this
        classifier = 'original'
    }

    shadowJar { // rename the shadowed jar, removing the suffix 'all'
        classifier = ''
    }

    repositories {
        mavenCentral()
        mavenLocal() // PluginMessageFramework

        maven {
            url 'https://hub.spigotmc.org/nexus/content/groups/public/'
        }

        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots'
        }

        maven { // Intake
            url 'http://maven.sk89q.com/artifactory/libs-snapshot-local'
        }

        maven { // Exhaust
            url 'http://repo.ellune.net/content/groups/public/'
        }
    }

    dependencies {
        testCompile group: 'junit', name: 'junit', version: '4.12'
    }

    test {
        testLogging.showStandardStreams = true

        minHeapSize = '128m'
        maxHeapSize = '512m'

        beforeTest { descriptor ->
            logger.lifecycle('Running test: ' + descriptor)
        }

        testLogging {
            exceptionFormat = 'full'
        }
    }

    rebel {
        rebelXmlDirectory = "/"
    }

    task('copyOutputsForTesting') {
        outputs.upToDateWhen { false }

        doLast {
            File sourceFile = project.shadowJar.archivePath;

            if (!sourceFile.exists()) {
                sourceFile = jar.outputs.files.files.getAt(0);
            }

            File targetFile = new File(parent.getProjectDir(), "testing/");
            if (targetFile.exists() && !targetFile.isDirectory()) {
                throw new RuntimeException("Path '" + targetFile.getPath() + "' is not a directory.");
            }

            if (project.getName().equals("core-bukkit")) {
                targetFile = new File(targetFile, "/bukkit/plugins/");
            } else if (project.getName().equals("core-bungee")) {
                targetFile = new File(targetFile, "/bungee/plugins");
            } else if (project.getName().startsWith("module-")) {
                targetFile = new File(targetFile, "/bukkit/plugins/Minecraftly/modules/")
            } else {
                return; // don't copy any other jars
            }

            targetFile.mkdirs();
            targetFile = new File(targetFile, sourceFile.getName());
            Files.copy(sourceFile.toPath(), targetFile.toPath(), StandardCopyOption.REPLACE_EXISTING);
        }
    }
}

group = 'com.minecraftly'

project(':core') {
    group = parent.getGroup() + '.core'
    version = '1.0-SNAPSHOT'

    dependencies {
        compile group: 'lc.vq', name: 'exhaust-core', version: exhaustVersion

        // HikariCP dependencies
        compile group: 'com.zaxxer', name: 'HikariCP-java6', version: '2.3.6'
        compile group: 'org.javassist', name: 'javassist', version: '3.19.0-GA'
        compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.10'
        compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.10'

        compile group: 'commons-dbutils', name: 'commons-dbutils', version: '1.6'
        compile group: 'com.ikeirnez.pluginmessageframework', name: 'pmf-core', version: parent.ext.pmfVersion
    }

    shadowJar {
        dependencies {
            include(dependency('lc.vq:exhaust-core'))

            // include HikariCP dependencies
            include(dependency('com.zaxxer:HikariCP-java6'))
            include(dependency('org.javassist:javassist'))
            include(dependency('org.slf4j:slf4j-api'))
            include(dependency('org.slf4j:slf4j-simple'))

            include(dependency('commons-dbutils:commons-dbutils'))
            include(dependency('com.ikeirnez.pluginmessageframework:pmf-core'))
        }
    }

    build.dependsOn(shadowJar)
}

project(':core-bukkit') {
    ext.cleanName = 'Minecraftly'
    group = project(':core').getGroup() + '.bukkit'
    version = '1.0-SNAPSHOT'

    dependencies {
        compile project(path: ':core', configuration: 'shadow')
        compile group: 'org.spigotmc', name: 'spigot-api', version: parent.ext.bukkitVersion
        compile group: 'lc.vq', name: 'exhaust-bukkit', version: parent.ext.exhaustVersion
        compile group: 'com.ikeirnez.pluginmessageframework', name: 'pmf-bukkit', version: parent.ext.pmfVersion
        compile group: 'com.ikeirnez.pluginmessageframework', name: 'pmf-packets-bungeecord', version: parent.ext.pmfVersion
    }

    shadowJar {
        dependencies {
            include(dependency(':core'))
            include(dependency('lc.vq:exhaust-bukkit'))
            include(dependency('com.ikeirnez.pluginmessageframework:pmf-bukkit'))
            include(dependency('com.ikeirnez.pluginmessageframework:pmf-packets-bungeecord'))
        }
    }

    build.dependsOn(shadowJar)
}

project(':core-bungee') {
    ext.cleanName = 'MinecraftlyBungee'
    group = project(':core').getGroup() + '.bungee'
    version = '1.0-SNAPSHOT'

    dependencies {
        compile project(path: ':core', configuration: 'shadow')
        compile group: 'net.md-5', name: 'bungeecord-api', version: parent.ext.bungeeVersion
        compile group: 'lc.vq', name: 'exhaust-bungee', version: parent.ext.exhaustVersion
        compile group: 'com.ikeirnez.pluginmessageframework', name: 'pmf-bungeecord', version: parent.ext.pmfVersion
    }

    shadowJar {
        dependencies {
            include(dependency(':core'))
            include(dependency('lc.vq:exhaust-bungee'))
            include(dependency('com.ikeirnez.pluginmessageframework:pmf-bungeecord'))
        }
    }

    build.dependsOn(shadowJar)
}

project(':module-world') {
    ext.cleanName = 'World'
    group = parent.getGroup() + '.modules.world'
    version = '1.0-SNAPSHOT'

    dependencies {
        compile group: 'org.spigotmc', name: 'spigot-api', version: parent.ext.bukkitVersion
        compile project(':core-bukkit')
    }
}

project(':module-chat') {
    ext.cleanName = 'Chat'
    group = parent.getGroup() + '.modules.chat'
    version = '1.0-SNAPSHOT'

    dependencies {
        compile group: 'org.spigotmc', name: 'spigot-api', version: parent.ext.bukkitVersion
        compile project(':core-bukkit')
    }
}

project(':module-survival-worlds') {
    ext.cleanName = 'Survival Worlds'
    group = parent.getGroup() + '.modules.survivalworlds'
    version = '1.0-SNAPSHOT'

    dependencies {
        compile group: 'org.spigotmc', name: 'spigot-api', version: parent.ext.bukkitVersion
        compile project(':core-bukkit')
    }
}

project(':module-spawn') {
    project.ext.cleanName = 'Spawn'
    group = parent.getGroup() + '.modules.spawn'
    version = '1.0-SNAPSHOT'

    dependencies {
        compile group: 'org.spigotmc', name: 'spigot-api', version: parent.ext.bukkitVersion
        compile project(':core-bukkit')
    }
}

subprojects { // this needs done after projects have been initialized
    def cleanName = project.ext.cleanName
    archivesBaseName = cleanName

    processResources {
        filter ReplaceTokens, tokens: [
                'project.group'  : project.getGroup(),
                'project.name'   : project.getName(),
                'project.version': project.getVersion(),
                'project.cleanName': cleanName
        ]
    }
}

idea {
    project {
        languageLevel = '1.7'
    }
}